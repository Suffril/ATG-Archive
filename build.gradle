buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'

def getGitHash = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', '--short', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

version = "1.0"+"-SNAPSHOT_"+getGitHash()
group = "com.lcm.doctorwho"
archivesBaseName = "lcm-doctorwho"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
	version = "1.12.2-14.23.2.2625"
	runDir = "run"
	mappings = "snapshot_20171003"
	makeObfSourceJar = false
}

repositories {
	maven { url "https://jitpack.io" }
}

dependencies {
	deobfCompile ('com.gitlab.lcoremodders:LucraftCore:1.12-2.0-SNAPSHOT') {
		exclude group: 'slimeknights.mantle'
		exclude group: 'slimeknights'
		exclude group: 'cofh'
		exclude group: 'mezz.jei'
	}
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version
	
	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		
		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
		
	// copy everything else except the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
